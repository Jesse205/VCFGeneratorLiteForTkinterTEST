; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "VCF Generator Lite"
#define MyAppPublisher "Jesse205"
#define MyAppExeName "vcf_generator_lite.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{2CA151A5-049F-4AD9-92B1-5F0A244F52A4}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={cm:MyAppName} {#MyAppVersion}
AppCopyright={#MyAppCopyright}
AppPublisher={cm:MyAppPublisher}
AppPublisherURL={cm:MyAppPublisherURL}
AppReadmeFile={cm:MyAppReadmeFile}
AppUpdatesURL={cm:MyAppUpdatesURL}
DefaultDirName={autopf}\{#MyAppPublisher}\{#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=.\LICENSE
PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
OutputDir=.\dist
OutputBaseFilename={#OutputBaseFilename}
Compression=lzma2/max
SolidCompression=yes
WizardStyle=modern
ArchitecturesAllowed=x64compatible
ArchitecturesInstallIn64BitMode=x64compatible
AllowNoIcons=yes
UninstallDisplayIcon={app}\{#MyAppExeName}
VersionInfoOriginalFileName={#OutputBaseFilename}.exe
VersionInfoVersion={#MyAppVersion}.0
VersionInfoCompany={#MyAppPublisher}
ShowLanguageDialog=auto

[Languages]
Name: en_us; MessagesFile: "compiler:Default.isl"
Name: zh_cn; MessagesFile: ".innosetup\Languages\ChineseSimplified.isl"

[LangOptions]
; Microsoft's recommended normal font size is 9pt.
DialogFontSize=9
WelcomeFontSize=12
TitleFontSize=29
CopyrightFontSize=9

; The language file does not contain the correct font, so we should set the correct font.
; https://learn.microsoft.com/zh-cn/windows/win32/uxguide/text-ui#guidelines
en_us.DialogFontName=Segoe UI
en_us.WelcomeFontName=Segoe UI
en_us.TitleFontName=Segoe UI
en_us.CopyrightFontName=Segoe UI
; Windows 7 does not have Microsoft Yahei UI fonts, so it should always be Microsoft Yahei fonts
zh_cn.DialogFontName=Microsoft Yahei
zh_cn.WelcomeFontName=Microsoft Yahei
zh_cn.TitleFontName=Microsoft Yahei
zh_cn.CopyrightFontName=Microsoft Yahei

[CustomMessages]
MyAppName=VCF Generator Lite
en_us.MyAppName=VCF Generator Lite
en_us.MyAppPublisher=Jesse205
en_us.MyAppPublisherURL=https://github.com/Jesse205
en_us.MyAppReadmeFile=https://github.com/HelloTool/VCFGeneratorLiteForTkinter/blob/master/README.md
en_us.MyAppUpdatesURL=https://github.com/HelloTool/VCFGeneratorLiteForTkinter/releases
en_us.SetupAppRunningOnWin7=Note: You are using Windows 7, you will need to add the following additional files after the installation is complete in order to run: python313.dll and api-ms-win-core-path-l1-1-0.dll.
en_us.UninstallFailed=Uninstall failed

zh_cn.MyAppName=VCF 生成器 Lite
zh_cn.MyAppPublisher=Jesse205
zh_cn.MyAppPublisherURL=https://gitee.com/Jesse205
zh_cn.MyAppReadmeFile=https://gitee.com/HelloTool/VCFGeneratorLiteForTkinter/blob/master/README.md
zh_cn.MyAppUpdatesURL=https://gitee.com/HelloTool/VCFGeneratorLiteForTkinter/releases
zh_cn.SetupAppRunningOnWin7=注意: 您正在使用 Windows 7, 您需要在安装完成后额外添加以下文件才能运行：python313.dll 和 api-ms-win-core-path-l1-1-0.dll。
zh_cn.UninstallFailed=自动卸载失败

[Code]

function GetUninstallString(): String;
var
  UninstallPath: String;
begin
  UninstallPath :=
    'Software\Microsoft\Windows\CurrentVersion\Uninstall\' +
    '{2CA151A5-049F-4AD9-92B1-5F0A244F52A4}_is1';
  if RegQueryStringValue(HKA, UninstallPath, 'UninstallString', Result) then
    Exit;

  Result := '';
end;

procedure CurStepChanged(CurStep: TSetupStep);
var
  UninstallCmd: String;
  ResultCode: Integer;
  ErrorMessage: String;
begin
  if (CurStep = ssInstall) then
  begin
    UninstallCmd := GetUninstallString();
    if UninstallCmd <> '' then
    begin
      UninstallCmd := RemoveQuotes(UninstallCmd);
      if FileExists(UninstallCmd) then
      begin
        if not Exec(UninstallCmd, '/SILENT /NORESTART /SUPPRESSMSGBOXES', '',
            SW_HIDE, ewWaitUntilTerminated, ResultCode) then
        begin
          ErrorMessage := CustomMessage('UninstallFailed') + #13#10 +
            SysErrorMessage(ResultCode) + ' (' + IntToStr(ResultCode) + ')';

          MsgBox(ErrorMessage, mbError, MB_OK);
        end;
      end;
    end;
  end;
end;

var
  Win7MessageShown: Boolean;

procedure InitializeWizard();
begin
  Win7MessageShown := False;
end;

procedure CurInstallProgressChanged(CurProgress, MaxProgress: Integer);
var
  WinVersion: TWindowsVersion;
  VersionStr: string;
begin
  GetWindowsVersionEx(WinVersion);
  if (CurProgress=MaxProgress) and (WinVersion.Major = 6) and (WinVersion.Minor = 1) and (not Win7MessageShown) then
  begin
    MsgBox(CustomMessage('SetupAppRunningOnWin7'), mbInformation, MB_OK);
    Win7MessageShown := True;
  end;
end;

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: ".\dist\vcf_generator_lite\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{cm:MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{cm:MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{cm:MyAppName}}"; Flags: nowait postinstall skipifsilent

